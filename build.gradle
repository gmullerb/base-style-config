//  Copyright (c) 2018 Gonzalo MÃ¼ller Bravo.
//  Licensed under the MIT License (MIT), see LICENSE.txt

plugins {
  id 'checkstyle'
  id 'codenarc'
  id 'groovy'
}

repositories {
  jcenter()
}

dependencies {
  testCompile 'org.codehaus.groovy:groovy-all:+'
  testCompile 'org.junit.jupiter:junit-jupiter-api:+'
  testRuntime 'org.junit.jupiter:junit-jupiter-engine:+'
}

final ROOT_FOLDER = '.'
final CODE_FILES = ['*.gradle', '*.groovy', 'src/**/*.groovy']

checkstyle {
  toolVersion = CHECKSTYLE_VERSION
}

codenarc {
  toolVersion = CODENARC_VERSION
}

// TASKS
////////

task assess (type: Checkstyle) {
  // Checkstyle task settings
  configFile = file('src/config/common/common-checks.xml')
  classpath = files('dummy') // Required by Checkstyle, Not required for Checker modules
  source fileTree(ROOT_FOLDER) {
    include CODE_FILES + ['src/config/', '*.gitignore', '*.md', '*.properties', '*.txt']
  }
  // gradle task settings
  description = 'Runs Checkstyle analysis for all files.'
  group = GROUP_ASSESS
}

task assessGradle(type: CodeNarc) {
  // CodeNarc task settings
  configFile = file('src/config/groovy/groovy-rules.groovy')
  source fileTree(ROOT_FOLDER) {
    include CODE_FILES
    exclude 'src/config/**/*'
  }
  // gradle task settings
  description = 'Run Codenarc analysis for all gradle files.'
  group = GROUP_ASSESS
}

task assembleArtifact (type: Jar) {
  // Jar task settings
  baseName = project.name
  version = project.version
  from 'src/config'
  destinationDir = file("$project.buildDir/artifact")
  // gradle task settings
  description = 'Creates a JAR file with all check configurations.'
  group = 'build'
  shouldRunAfter = ['assess', 'assessGradle', 'test']
}

test {
  // Test task settings
  useJUnitPlatform()
  inputs.dir 'src'
}

// Default task
///////////////

defaultTasks 'assess', 'assessGradle', 'test', 'assembleArtifact'
