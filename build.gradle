//  Copyright (c) 2018 Gonzalo MÃ¼ller Bravo.
//  Licensed under the MIT License (MIT), see LICENSE.txt

plugins {
  id 'checkstyle'
  id 'codenarc'
}

repositories {
  jcenter()
}

final ROOT_FOLDER = '.'
final CODE_FILES = ['*.gradle', '*.groovy']

// TASKS
////////

task assess (type: Checkstyle) {
  // Checkstyle task settings
  configFile = file('config/common/common-checks.xml')
  classpath = files('dummy') // Required by Checkstyle, Not required for Checker modules
  source files(ROOT_FOLDER) {
    include CODE_FILES + ['config/', '*.gitignore', '*.md', '*.properties', '*.txt']
  }
  // gradle task settings
  description = 'Runs Checkstyle analysis for all files.'
  group = GROUP_ASSESS
}

task assessGradle(type: CodeNarc) {
  // CodeNarc task settings
  configFile = file('config/gradle/gradle-rules.groovy')
  source files(ROOT_FOLDER) {
    include CODE_FILES
  }
  // gradle task settings
  description = 'Run Codenarc analysis for all gradle files.'
  group = GROUP_ASSESS
}

task build (type: Jar) {
  // Jar task settings
  baseName = project.name
  version = project.version
  from 'config'
  destinationDir project.buildDir
  // gradle task settings
  description = 'Creates a JAR file with all check configurations.'
  group = 'build'
  dependsOn = ['assess', 'assessGradle']
}

// Default task
///////////////

defaultTasks 'build'
